plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group 'xyz.oribuin.eternalcrates'
    version '1.0-beta1'

    targetCompatibility = JavaVersion.VERSION_16
    sourceCompatibility = JavaVersion.VERSION_16

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        maven { url "https://repo.rosewooddev.io/repository/public/" }
        maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
        maven { url "https://repo.codemc.org/repository/maven-public/" }
        maven { url "https://maven.enginehub.org/repo/" }

        repositories {
            maven {
                url = 'https://repo.rosewooddev.io/repository/maven-public'
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                    authentication {
                        basic(BasicAuthentication)
                    }
                }
            }
        }
    }
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS:Wrapper')
    implementation project(':NMS:v1_16_R1')
    implementation project(':NMS:v1_16_R2')
    implementation project(':NMS:v1_16_R3')
    implementation project(path: ':NMS:v1_17_R1', configuration: 'remapped')
}

shadowJar {
    archiveClassifier.set(null)
    relocate "xyz.oribuin.orilibrary", "${project.group}.libs.cmdlib"
    relocate "xyz.oribuin.gui", "${project.group}.libs.gui"
    relocate "com.zaxxer.hikaricp", "${project.group}.libs.hikaricp"
    relocate "org.jetbrains", "${project.group}.libs.jetbrains"
}

tasks.getByName("build").dependsOn("shadowJar")