plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java-library'
}

group 'xyz.oribuin.eternalcrates'
version '1.0.5-pre1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
}


repositories {
    mavenCentral()
    mavenLocal()

    maven { url 'https://libraries.minecraft.net' }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.rosewooddev.io/repository/public/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url 'https://jitpack.io' }
    maven { url 'https://erethon.de/repo/' }
    maven { url 'https://mvn.lumine.io/repository/maven-public/' }

    maven {
        url = 'https://repo.rosewooddev.io/repository/maven-public'
        credentials {
            username project.mavenUser
            password project.mavenPassword
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'com.mojang:authlib:1.5.21'

    // Frameworks & MySQL APIs
    implementation 'dev.rosewood:rosegarden:1.1.0.48-SNAPSHOT'
    implementation 'io.github.bananapuncher714:nbteditor:7.18.2'
    implementation('dev.triumphteam:triumph-gui:3.1.3') {
        exclude group: 'com.google.code.gson', module: 'gson' // Already included in spigot-api
        exclude group: "net.kyori", module: "*" // Already included in paper-api
    }

    // Custom Item Plugins
    compileOnly 'com.github.jojodmo:ItemBridge:master-SNAPSHOT'
    compileOnly 'com.github.emanondev:ItemEdit:2.18'
    compileOnly 'com.github.LoneDev6:api-itemsadder:2.5.5'
    compileOnly 'nl.knokko:customitems:9.18'
    compileOnly 'net.Indyuce:MMOItems:6.7'
    compileOnly 'com.github.oraxen:oraxen:afc4903680'
    compileOnly 'com.github.Slimefun:Slimefun4:RC-30'
    compileOnly 'thirtyvirus.uber:uberitems:2.6'
    compileOnly 'io.lumine:Mythic-Dist:5.0.1-SNAPSHOT'
}

shadowJar {
    archiveClassifier.set(null)
//    minimize() // This removes the IntegerRoseCommandArgumentHandler class, which is needed for the plugin to work.

    relocate "org.jetbrains", "${project.group}.libs.jetbrains"
    relocate "dev.rosewood.rosegarden", "${project.group}.libs.rosegarden"
    relocate "io.github.bananapuncher714.nbteditor", "${project.group}.libs.nbteditor"
    relocate "dev.triumphteam.gui", "${project.group}.libs.triumph-gui"

}

tasks.getByName("build").dependsOn("shadowJar")
